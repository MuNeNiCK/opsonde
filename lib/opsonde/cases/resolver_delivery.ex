defmodule Opsonde.Cases.ResolverDelivery do
  @moduledoc false

  require Ash.Query

  alias Opsonde.{Cases, Providers}

  alias Opsonde.Cases.{
    AIInvocation,
    AIInvocationClaim,
    Budget,
    Case,
    CaseEvent,
    ResolutionRun,
    ResolverProjection,
    Turn
  }

  alias Opsonde.Providers.AI

  @max_result_bytes 65_536

  @spec run(String.t(), keyword()) ::
          :ok | {:cancel, String.t()} | {:snooze, pos_integer()} | {:error, term()}
  def run(turn_id, opts \\ []) do
    with {:ok, turn} <- Cases.get_turn(turn_id, authorize?: false) do
      if turn.status == :completed do
        :ok
      else
        deliver(turn, opts)
      end
    end
  end

  defp deliver(turn, opts) do
    with {:ok, selection} <- assigned_selection(turn),
         {:ok, selection} <- current_selection(selection),
         target_invocation <- invocation(turn.case_id, Keyword.get(opts, :target_invocation, %{})),
         {:ok, request} <- ResolverProjection.build(turn.id, selection, target_invocation) do
      resolve(turn, selection, request, opts)
    else
      {:error, error} -> handle_failure(turn, error)
    end
  end

  defp resolve(turn, selection, request, opts) do
    case claim_invocation(turn, selection, request) do
      {:ok, %AIInvocationClaim{state: :claimed} = claim} ->
        dispatch(turn, selection, request, claim, opts)

      {:ok, %AIInvocationClaim{state: :interrupted, invocation: invocation}} ->
        handle_interruption(turn, invocation)

      {:ok, %AIInvocationClaim{state: :terminal}} ->
        if turn_completed?(turn.id), do: :ok, else: {:error, "AI invocation is already terminal"}

      {:error, error} ->
        if resolver_context_current?(request),
          do: handle_failure(turn, error),
          else: retry_changed_context(turn)
    end
  end

  defp dispatch(turn, selection, request, claim, opts) do
    ai_invocation =
      resolver_invocation(request, Keyword.get(opts, :ai_invocation, %{}))

    case Providers.ai_resolve(selection.provider_id, request, ai_invocation, authorize?: false) do
      {:ok, decision} ->
        accept_decision(turn, selection, request, claim.invocation, decision)

      {:error, error} ->
        if resolver_context_current?(request) do
          handle_failure(turn, error, claim.invocation)
        else
          with {:ok, _invocation} <-
                 record_invocation(claim.invocation, :failed, category: "context_changed") do
            retry_changed_context(turn)
          end
        end
    end
  end

  defp accept_decision(turn, selection, request, invocation, decision) do
    if resolver_context_current?(request) do
      with {:ok, result} <- result(decision, selection, request),
           :ok <- valid_result_size(result),
           {:ok, _result} <- accept(turn, invocation, decision, result, request) do
        :ok
      else
        {:error, error} -> handle_failure(turn, error, invocation)
      end
    else
      with {:ok, _result} <- settle_unused_result(turn, invocation, decision, "context_changed") do
        retry_changed_context(turn)
      end
    end
  end

  defp claim_invocation(turn, selection, request) do
    with {:ok, incident} <- Cases.get_case(turn.case_id, authorize?: false),
         true <- resolver_context_matches?(incident, request) || :context_changed do
      Cases.claim_ai_invocation(
        :resolver,
        turn.case_id,
        incident.revision,
        turn.resolution_run_id,
        turn.id,
        turn.revision,
        nil,
        nil,
        selection.provider_id,
        selection.assignment_id,
        selection.provider_revision,
        selection.assignment_revision,
        selection.source,
        AIInvocation.request_digest(request),
        authorize?: false
      )
    end
  end

  defp assigned_selection(turn) do
    key = assignment_key(turn.id)

    case Cases.case_event_by_idempotency(turn.case_id, key,
           authorize?: false,
           not_found_error?: false
         ) do
      {:ok, %CaseEvent{} = event} ->
        selection_from_event(event, turn)

      {:ok, nil} ->
        create_assignment(turn, key)

      {:error, error} ->
        {:error, error}
    end
  end

  defp create_assignment(turn, key) do
    with {:ok, %AI.Selection{role: :resolver} = selection} <-
           Providers.select_resolver_ai(authorize?: false),
         {:ok, _event} <- create_assignment_event(turn, key, selection) do
      {:ok, selection}
    else
      {:error, _error} = error ->
        case Cases.case_event_by_idempotency(turn.case_id, key,
               authorize?: false,
               not_found_error?: false
             ) do
          {:ok, %CaseEvent{} = event} -> selection_from_event(event, turn)
          _missing -> error
        end
    end
  end

  defp create_assignment_event(turn, key, selection) do
    Cases.create_case_event_record(
      %{
        case_id: turn.case_id,
        resolution_run_id: turn.resolution_run_id,
        event_type: "resolver_assigned",
        idempotency_key: key,
        data: %{
          "turn_id" => turn.id,
          "provider_id" => selection.provider_id,
          "provider_revision" => selection.provider_revision,
          "assignment_id" => selection.assignment_id,
          "assignment_revision" => selection.assignment_revision,
          "source" => to_string(selection.source)
        }
      },
      authorize?: false
    )
  end

  defp selection_from_event(
         %CaseEvent{
           resolution_run_id: run_id,
           data: %{
             "turn_id" => turn_id,
             "provider_id" => provider_id,
             "provider_revision" => provider_revision,
             "assignment_id" => assignment_id,
             "assignment_revision" => assignment_revision,
             "source" => "assignment"
           }
         },
         turn
       )
       when run_id == turn.resolution_run_id and turn_id == turn.id and is_binary(provider_id) and
              is_integer(provider_revision) and provider_revision > 0 and is_binary(assignment_id) and
              is_integer(assignment_revision) and assignment_revision > 0 do
    {:ok,
     %AI.Selection{
       role: :resolver,
       provider_id: provider_id,
       provider_revision: provider_revision,
       source: :assignment,
       assignment_id: assignment_id,
       assignment_revision: assignment_revision
     }}
  end

  defp selection_from_event(_event, _turn),
    do: {:error, ai_error(:invalid_input, "Persisted Resolver assignment is invalid")}

  defp current_selection(selection) do
    with {:ok, assignment} <-
           Providers.load_resolver_ai_usage_role_assignment(
             selection.assignment_id,
             selection.assignment_revision,
             selection.provider_revision,
             authorize?: false
           ),
         true <-
           assignment.provider_id == selection.provider_id ||
             {:error, ai_error(:unavailable, "Resolver AI assignment changed")} do
      {:ok, selection}
    else
      {:error, %AI.Error{} = error} -> {:error, error}
      {:error, _error} -> {:error, ai_error(:unavailable, "Resolver AI is unavailable")}
    end
  end

  defp invocation(case_id, supplied) do
    supplied_cancelled = Map.get(supplied, :cancelled?)

    Map.put(supplied, :cancelled?, fn ->
      cancelled?(supplied_cancelled) or case_cancelled?(case_id)
    end)
  end

  defp resolver_invocation(request, supplied) do
    supplied_cancelled = Map.get(supplied, :cancelled?)

    Map.put(supplied, :cancelled?, fn ->
      cancelled?(supplied_cancelled) or not resolver_context_current?(request)
    end)
  end

  defp resolver_context_current?(request) do
    case Cases.get_case(request.case_id, authorize?: false) do
      {:ok, incident} ->
        resolver_context_matches?(incident, request)

      {:error, _error} ->
        false
    end
  end

  defp resolver_context_matches?(incident, request) do
    incident.status == :running and not incident.cancel_requested and
      incident.alert_state == request.alert_state and
      incident.selected_target_id == request.selected_target_id and
      incident.selected_target_revision == request.selected_target_revision
  end

  defp retry_changed_context(turn) do
    cond do
      case_cancelled?(turn.case_id) -> {:cancel, "Case resolution was cancelled"}
      turn_completed?(turn.id) -> :ok
      true -> {:snooze, 1}
    end
  end

  defp cancelled?(callback) when is_function(callback, 0), do: callback.()
  defp cancelled?(_callback), do: false

  defp case_cancelled?(case_id) do
    case Cases.get_case(case_id, authorize?: false) do
      {:ok, %{cancel_requested: true}} -> true
      {:ok, %{status: status}} when status != :running -> true
      {:ok, _incident} -> false
      {:error, _error} -> true
    end
  end

  defp accept(turn, invocation, decision, result, request) do
    usage_units = decision.usage.input_tokens + decision.usage.output_tokens
    progress_kind = progress_kind(decision.intent)

    Ash.transact([AIInvocation, Case, ResolutionRun, Turn, CaseEvent], fn ->
      with {:ok, incident} <- lock_case(turn.case_id),
           true <- resolver_context_matches?(incident, request) || :context_changed,
           {:ok, usage_result} <-
             charge_usage(turn, usage_units, "resolver-result:#{invocation.id}"),
           true <-
             usage_result.status in [:charged, :duplicate] ||
               {:error, "AI usage limit exhausted"},
           {:ok, completion} <-
             Cases.complete_turn(
               turn.id,
               turn.revision,
               result,
               progress_kind,
               %{"action" => "route_resolver_decision", "turn_id" => turn.id},
               "Review the Resolver decision",
               authorize?: false
             ),
           {:ok, _invocation} <-
             record_invocation(invocation, :completed,
               input_tokens: decision.usage.input_tokens,
               output_tokens: decision.usage.output_tokens,
               result_digest: digest(result)
             ),
           {:ok, _job} <- enqueue_route(turn.id) do
        completion
      end
    end)
    |> accepted_or_existing(turn, invocation, decision)
  end

  defp lock_case(case_id) do
    Case
    |> Ash.Query.for_read(:read)
    |> Ash.Query.filter(id: case_id)
    |> Ash.Query.lock(:for_update)
    |> Ash.read_one(authorize?: false)
    |> case do
      {:ok, nil} -> {:error, "Case is unavailable"}
      result -> result
    end
  end

  defp charge_usage(turn, 0, _idempotency_key) do
    with {:ok, incident} <- Cases.get_case(turn.case_id, authorize?: false),
         {:ok, run} <- Cases.get_resolution_run(turn.resolution_run_id, authorize?: false) do
      {:ok, %Cases.BudgetResult{status: :charged, case: incident, run: run, value: run}}
    end
  end

  defp charge_usage(turn, usage_units, idempotency_key) do
    Cases.charge_resolution_run(
      turn.case_id,
      turn.resolution_run_id,
      :ai_usage,
      usage_units,
      idempotency_key,
      %{"action" => "review_ai_usage", "turn_id" => turn.id},
      "Increase the AI usage limit or review the Case",
      authorize?: false
    )
  end

  defp enqueue_route(turn_id) do
    %{"turn_id" => turn_id}
    |> Opsonde.Cases.DecisionRouteWorker.new()
    |> Oban.insert()
  end

  defp accepted_or_existing({:ok, result}, _turn, _invocation, _decision), do: {:ok, result}

  defp accepted_or_existing({:error, error}, turn, invocation, decision) do
    case Cases.get_turn(turn.id, authorize?: false) do
      {:ok, %{status: :completed} = completed} ->
        case settle_unused_result(turn, invocation, decision, "superseded") do
          {:ok, _invocation} -> {:ok, completed}
          {:error, settle_error} -> {:error, settle_error}
        end

      _unfinished ->
        {:error, error}
    end
  end

  defp result(decision, selection, request) do
    with {:ok, intent} <- intent(decision.intent, request) do
      {:ok,
       %{
         "outcome" => "decision",
         "intent" => intent,
         "usage" => %{
           "input_tokens" => decision.usage.input_tokens,
           "output_tokens" => decision.usage.output_tokens
         },
         "resolver" => %{
           "provider_id" => selection.provider_id,
           "provider_revision" => selection.provider_revision,
           "assignment_id" => selection.assignment_id,
           "assignment_revision" => selection.assignment_revision
         }
       }}
    end
  end

  defp intent(%AI.Proposal{} = value, request) do
    with {:ok, request_tool} <- proposal_tool(request, value.tool_id) do
      snapshot = value |> typed_intent() |> Map.put("tool", tool_snapshot(request_tool))

      case value.verification_intent do
        %AI.VerificationIntent{tool_id: tool_id} ->
          with {:ok, verification_tool} <- verification_tool(request, tool_id) do
            {:ok, Map.put(snapshot, "verification_tool", tool_snapshot(verification_tool))}
          end

        nil ->
          {:ok, Map.put(snapshot, "verification_tool", %{})}
      end
    end
  end

  defp intent(%AI.TargetTraversal{} = value, request) do
    with {:ok, relationship} <- relationship(request, value.relationship_id) do
      {:ok,
       value
       |> typed_intent()
       |> Map.put("relationship", relationship_snapshot(relationship))}
    end
  end

  defp intent(%_{} = value, _request), do: {:ok, typed_intent(value)}

  defp typed_intent(%module{} = value),
    do:
      value
      |> json_value()
      |> Map.put("type", module |> Module.split() |> List.last() |> Macro.underscore())

  defp observation_tool(request, tool_id) do
    case Enum.find(request.observation_tools, &(&1.id == tool_id)) do
      %AI.ObservationTool{} = tool ->
        {:ok, tool}

      _missing ->
        {:error, ai_error(:invalid_output, "AI observation tool snapshot is unavailable")}
    end
  end

  defp proposal_tool(request, tool_id) do
    case Enum.find(request.proposal_tools, &(&1.id == tool_id)) do
      %AI.ProposalTool{} = tool ->
        {:ok, tool}

      _missing ->
        {:error, ai_error(:invalid_output, "AI Proposal tool snapshot is unavailable")}
    end
  end

  defp verification_tool(request, tool_id) do
    observation_tool(request, tool_id)
  end

  defp relationship(request, relationship_id) do
    case Enum.find(request.target_relations, &(&1.id == relationship_id)) do
      %AI.TargetRelation{} = relationship ->
        {:ok, relationship}

      _missing ->
        {:error, ai_error(:invalid_output, "AI Target relationship snapshot is unavailable")}
    end
  end

  defp relationship_snapshot(relationship) do
    %{
      "id" => relationship.id,
      "revision" => relationship.revision,
      "source_target_id" => relationship.source_target.id,
      "source_target_revision" => relationship.source_target.revision,
      "destination_target_id" => relationship.destination_target.id,
      "destination_target_revision" => relationship.destination_target.revision,
      "kind" => relationship.kind
    }
  end

  defp tool_snapshot(%AI.ProposalTool{} = tool) do
    tool
    |> common_tool_snapshot()
    |> Map.put("request_kind", to_string(tool.request_kind))
  end

  defp tool_snapshot(tool), do: common_tool_snapshot(tool)

  defp common_tool_snapshot(tool) do
    %{
      "id" => tool.id,
      "target_id" => tool.target_id,
      "target_revision" => tool.target_revision,
      "access_method_id" => tool.access_method_id,
      "access_method_revision" => tool.access_method_revision,
      "provider_id" => tool.provider_id,
      "provider_revision" => tool.provider_revision,
      "capability" => tool.capability,
      "operation" => tool.operation
    }
  end

  defp json_value(%_{} = value), do: value |> Map.from_struct() |> json_value()

  defp json_value(value) when is_map(value),
    do: Map.new(value, fn {key, nested} -> {to_string(key), json_value(nested)} end)

  defp json_value(value) when is_list(value), do: Enum.map(value, &json_value/1)
  defp json_value(value) when value in [true, false, nil], do: value
  defp json_value(value) when is_atom(value), do: to_string(value)
  defp json_value(value), do: value

  defp progress_kind(%AI.Proposal{}), do: :proposal
  defp progress_kind(%AI.RecoveryConclusion{}), do: :source_change
  defp progress_kind(%AI.Handoff{}), do: :human_input
  defp progress_kind(_intent), do: :hypothesis

  defp valid_result_size(result) do
    case Jason.encode(result) do
      {:ok, encoded} when byte_size(encoded) <= @max_result_bytes -> :ok
      _invalid -> {:error, ai_error(:invalid_output, "AI Resolver result is too large")}
    end
  end

  defp handle_interruption(turn, invocation) do
    reason = "Resolver response was lost after dispatch; retrying autonomously"
    intent = %{"action" => "continue_resolution", "source_turn_id" => turn.id}

    Ash.transact([AIInvocation, Case, ResolutionRun, Turn, CaseEvent], fn ->
      with {:ok, usage_result} <-
             charge_usage(
               turn,
               invocation.reserved_units,
               "resolver-unknown:#{invocation.id}"
             ),
           {:ok, result} <- retry_interrupted(turn, invocation, usage_result, intent, reason) do
        result
      end
    end)
    |> case do
      {:ok, _incident} -> :ok
      {:error, error} -> if(turn_completed?(turn.id), do: :ok, else: {:error, error})
    end
  end

  defp retry_interrupted(_turn, _invocation, %{status: :exhausted} = result, _intent, _reason),
    do: {:ok, result}

  defp retry_interrupted(turn, invocation, %{status: status}, intent, reason)
       when status in [:charged, :duplicate] do
    with {:ok, completed} <-
           Cases.complete_turn(
             turn.id,
             turn.revision,
             %{
               "outcome" => "delivery_unknown",
               "category" => "response_unknown",
               "message" => reason,
               "reserved_usage_units" => invocation.reserved_units
             },
             :none,
             intent,
             "Continue autonomous resolution after the lost Resolver response",
             authorize?: false
           ),
         {:ok, next_turn} <-
           Cases.start_turn(
             turn.case_id,
             turn.resolution_run_id,
             "resolver:response-unknown:#{invocation.id}",
             %{
               "objective" => "Continue resolution after a lost Resolver response",
               "source" => "resolver_delivery_interruption",
               "source_turn_id" => turn.id,
               "ai_invocation_id" => invocation.id
             },
             intent,
             "Continue autonomous resolution after the lost Resolver response",
             authorize?: false
           ),
         {:ok, next_turn} <- set_retry_pending(next_turn, turn.id) do
      {:ok, %{completed: completed, next_turn: next_turn}}
    end
  end

  defp handle_failure(turn, error, invocation \\ nil) do
    cond do
      case_cancelled?(turn.case_id) ->
        {:cancel, "Case resolution was cancelled"}

      turn_completed?(turn.id) ->
        :ok

      true ->
        {category, message} = failure(error)
        rejection_code = rejection_code(error)

        case persist_failure(turn, invocation, category, message, rejection_code) do
          {:ok, _incident} ->
            :ok

          {:error, persistence_error} ->
            if turn_completed?(turn.id), do: :ok, else: {:error, persistence_error}
        end
    end
  end

  defp persist_failure(turn, invocation, category, message, rejection_code) do
    if retryable_failure?(category) do
      persist_retryable_failure(turn, invocation, category, message, rejection_code)
    else
      persist_attention_failure(turn, invocation, category, message)
    end
  end

  defp persist_retryable_failure(turn, invocation, category, message, rejection_code) do
    intent = %{"action" => "continue_resolution", "source_turn_id" => turn.id}

    Ash.transact([AIInvocation, Case, ResolutionRun, Turn, CaseEvent], fn ->
      with {:ok, incident} <- lock_case(turn.case_id),
           true <-
             (incident.status == :running and not incident.cancel_requested) ||
               {:error, "Case resolution is not running"},
           {:ok, completed} <-
             Cases.complete_turn(
               turn.id,
               turn.revision,
               %{
                 "outcome" => "delivery_failed",
                 "category" => category,
                 "rejection_code" => rejection_code,
                 "message" => String.slice(message, 0, 1_000)
               },
               :none,
               intent,
               "Review Resolver limits or continue the Case manually",
               authorize?: false
             ),
           {:ok, _invocation} <- record_failure(invocation, category),
           {:ok, next_turn} <-
             Cases.start_turn(
               turn.case_id,
               turn.resolution_run_id,
               "resolver:delivery-retry:#{turn.id}",
               retry_turn_intent(turn, category, rejection_code),
               intent,
               "Review Resolver limits or continue the Case manually",
               authorize?: false
             ),
           {:ok, next_turn} <- set_retry_pending(next_turn, turn.id) do
        %{completed: completed, next_turn: next_turn}
      end
    end)
  end

  defp retryable_failure?(category),
    do: category in ["timeout", "unreachable", "rate_limited", "invalid_output", "failed"]

  defp retry_turn_intent(turn, category, rejection_code) do
    %{
      "objective" => "Continue resolution after a retryable Resolver delivery failure",
      "source" => "resolver_delivery_failure",
      "source_turn_id" => turn.id,
      "category" => category
    }
    |> then(fn intent ->
      if is_binary(rejection_code) and rejection_code != "",
        do: Map.put(intent, "rejection_code", rejection_code),
        else: intent
    end)
  end

  defp set_retry_pending(%{status: :exhausted} = result, _source_turn_id), do: {:ok, result}

  defp set_retry_pending(
         %{status: status, case: incident, value: next_turn} = result,
         source_turn_id
       )
       when status in [:charged, :duplicate] do
    pending = %{
      "action" => "resolve_turn",
      "turn_id" => next_turn.id,
      "source_turn_id" => source_turn_id
    }

    if incident.pending_intent == pending do
      {:ok, result}
    else
      with {:ok, _updated} <-
             Cases.update_case_record(
               incident,
               incident.revision,
               %{pending_intent: pending, stop_reason: nil, required_human_input: nil},
               authorize?: false
             ) do
        {:ok, result}
      end
    end
  end

  defp persist_attention_failure(turn, invocation, category, message) do
    reason = String.slice("Resolver delivery #{category}: #{message}", 0, 500)
    intent = %{"action" => "retry_resolver", "turn_id" => turn.id}

    Ash.transact([AIInvocation, Case, ResolutionRun, Turn, CaseEvent], fn ->
      with {:ok, completed} <-
             Cases.complete_turn(
               turn.id,
               turn.revision,
               %{
                 "outcome" => "delivery_failed",
                 "category" => category,
                 "message" => String.slice(message, 0, 1_000)
               },
               :human_input,
               intent,
               "Review the Resolver delivery failure",
               authorize?: false
             ),
           {:ok, _invocation} <- record_failure(invocation, category),
           {:ok, incident} <-
             Cases.require_case_attention(
               turn.case_id,
               completed.case.revision,
               turn.resolution_run_id,
               completed.run.revision,
               "resolver-failure:#{turn.id}",
               reason,
               intent,
               "Review the Resolver delivery failure",
               authorize?: false
             ) do
        incident
      end
    end)
  end

  defp settle_unused_result(turn, invocation, decision, category) do
    amount = decision.usage.input_tokens + decision.usage.output_tokens

    Ash.transact([AIInvocation, Case, ResolutionRun, CaseEvent], fn ->
      with {:ok, charged} <-
             charge_usage(turn, amount, "resolver-result:#{invocation.id}"),
           true <-
             charged.status in [:charged, :duplicate] ||
               {:error, "AI usage limit exhausted"},
           {:ok, invocation} <-
             record_invocation(invocation, :completed,
               input_tokens: decision.usage.input_tokens,
               output_tokens: decision.usage.output_tokens,
               category: category,
               result_digest: digest(decision)
             ) do
        invocation
      end
    end)
  end

  defp record_failure(nil, _category), do: {:ok, nil}

  defp record_failure(invocation, category),
    do: record_invocation(invocation, :failed, category: category)

  defp record_invocation(invocation, status, attrs) do
    Cases.record_ai_invocation_outcome(
      invocation,
      invocation.revision,
      %{
        status: status,
        input_tokens: Keyword.get(attrs, :input_tokens, 0),
        output_tokens: Keyword.get(attrs, :output_tokens, 0),
        category: Keyword.get(attrs, :category),
        result_digest: Keyword.get(attrs, :result_digest),
        completed_at: DateTime.utc_now()
      },
      authorize?: false
    )
  end

  defp digest(value) do
    value
    |> :erlang.term_to_binary([:deterministic])
    |> then(&:crypto.hash(:sha256, &1))
    |> Base.encode16(case: :lower)
  end

  defp failure(error) do
    case find_error(error) do
      %AI.Error{category: category} -> {to_string(category), public_failure(category)}
      _error -> {"failed", "Resolver delivery failed"}
    end
  end

  defp public_failure(:authentication), do: "Resolver authentication failed"
  defp public_failure(:unreachable), do: "Resolver AI is unreachable"
  defp public_failure(:timeout), do: "Resolver AI timed out"
  defp public_failure(:rate_limited), do: "Resolver AI rate limit was exceeded"
  defp public_failure(:cancelled), do: "Resolver decision was cancelled"
  defp public_failure(:invalid_input), do: "Resolver input is invalid"
  defp public_failure(:invalid_output), do: "Resolver output is invalid"
  defp public_failure(:unavailable), do: "Resolver AI is unavailable"
  defp public_failure(_category), do: "Resolver delivery failed"

  defp find_error(%AI.Error{} = error), do: error

  defp find_error(%{errors: errors}) when is_list(errors),
    do: Enum.find_value(errors, &find_error/1)

  defp find_error(error), do: error

  defp rejection_code(error) do
    case find_error(error) do
      %AI.Error{category: :invalid_output, message: message} -> invalid_output_code(message)
      _error -> nil
    end
  end

  defp invalid_output_code("AI provider returned no JSON text"), do: "missing_json_text"
  defp invalid_output_code("AI provider JSON text is too large"), do: "json_text_too_large"
  defp invalid_output_code("AI provider output is not valid JSON"), do: "json_decode"

  defp invalid_output_code("AI provider JSON does not match the requested schema"),
    do: "schema_validation"

  defp invalid_output_code("AI provider did not return a structured object"),
    do: "missing_structured_object"

  defp invalid_output_code("AI provider output is too large"), do: "output_too_large"
  defp invalid_output_code("AI provider did not return token usage"), do: "usage_missing"
  defp invalid_output_code("AI token usage is invalid"), do: "usage_invalid"
  defp invalid_output_code("AI Resolver output is too large"), do: "output_too_large"
  defp invalid_output_code("AI Resolver must conclude recovery"), do: "recovery_required"
  defp invalid_output_code("AI Target search is invalid"), do: "target_search"
  defp invalid_output_code("AI Target selection is invalid"), do: "target_selection"
  defp invalid_output_code("AI Target traversal is invalid"), do: "target_traversal"
  defp invalid_output_code("AI Proposal is invalid"), do: "proposal"
  defp invalid_output_code("AI recovery conclusion is invalid"), do: "recovery"
  defp invalid_output_code("AI handoff is invalid"), do: "handoff"
  defp invalid_output_code("AI Resolver intent is invalid"), do: "resolver_intent"
  defp invalid_output_code(_message), do: "invalid_output"

  defp turn_completed?(turn_id) do
    match?({:ok, %{status: :completed}}, Cases.get_turn(turn_id, authorize?: false))
  end

  defp assignment_key(turn_id), do: Budget.key("turn:resolver_assignment", turn_id)
  defp ai_error(category, message), do: AI.Error.exception(category: category, message: message)
end
